description = "Xoom FeatherCon Servlet Container"

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'

/*
The format of the bintray.gradle file is

ext.bintray =[
   user: "theuser",
   apikey: "thekey"
]

This file must exist in the project top-level directory.

 */
apply from: file("${rootDir}/bintray.gradle")

group = "com.xoom"
version = "1.0"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-jacoco:0.3.0'
    }
}

repositories {
    mavenCentral()
}

ext.versions = [
        jersey: "1.17",
        slf4j: "1.7.5",
        logback: "1.0.13",
        junit: "4.11"
]

dependencies {
    compile "ch.qos.logback:logback-classic:${versions.logback}"
    compile "org.slf4j:jul-to-slf4j:${versions.slf4j}"
    compile 'org.eclipse.jetty:jetty-server:8.1.9.v20130131'
    compile 'org.eclipse.jetty:jetty-servlet:8.1.9.v20130131'
    compile 'org.eclipse.jetty.orbit:javax.servlet:3.0.0.v201112011016'

    testCompile group: "junit", name: "junit", version: versions.junit
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "com.sun.jersey:jersey-core:${versions.jersey}"
    testCompile "com.sun.jersey:jersey-json:${versions.jersey}"
    testCompile "com.sun.jersey:jersey-server:${versions.jersey}"
    testCompile "com.sun.jersey:jersey-servlet:${versions.jersey}"
    testCompile "com.sun.jersey:jersey-client:${versions.jersey}"
}

tasks.withType(Test) {
    systemProperties = System.getProperties()
    jvmArgs '-Xmx2g'
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
    }
}

tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-http:2.4'
}

uploadArchives {
    repositories {
        mavenDeployer {
            name = 'xoom-xoomoss-feathercon'
            configuration = configurations.deployerJars
            repository(url: "https://api.bintray.com/maven/xoom/xoomoss/feathercon") {
                authentication(userName: bintray.user, password: bintray.apikey)
            }
        }
    }
}
